/*
 * Global build file used by all OpenSHA subprojects.
 * Declares common fields and tasks.
 * 
 * This file requires that the 'parentProject' property be defined (should be null for top level project)
 */


sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11
compileJava.options.encoding = "UTF-8"

repositories {
    mavenCentral()
}

configurations {
    apiResolvable {
        description 'resolvable view of the api classpath'
        canBeResolved=true

        extendsFrom api
    }
}

configurations {
    implResolvable {
        description 'resolvable view of the implementation classpath'
        canBeResolved=true

        extendsFrom implementation
    }
}

ext.getDate = {
    new Date().format('yyyy_MM_dd')
}

ext.getGitHash = { ->
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
        }
        return stdout.toString().trim()
    } catch (Exception e) {
        return ""
    }
    
}

//System.out.println('running settings with project='+project.name+' and rootProject.name='+rootProject.name)
if (project.parentProject != null) {
    System.out.println(project.name+" is a child of: "+project.parentProject)
} else {
//    System.out.println(project.name+" is top level")
}

if (project.parentProject != null) {
//    System.out.println('Defining fatJar task in '+project.name+" that depends on "+project.parentProject)
    task fatJar(type: Jar, dependsOn: ':'+project.parentProject+':fatJar') {
        archiveBaseName = project.name + '-all'
        // include all 'api' dependencies
        from {
            configurations.apiResolvable.collect { it.isDirectory() ? it : zipTree(it).matching {
                exclude { it.path.contains('META-INF') }
            }}
        }
        // include compiled source from this project
        from sourceSets.main.allJava
        // include upstream project fat jar
        from zipTree(file('../'+project.parentProject+'/build/libs/'+project.parentProject+'-all.jar'))
    
        duplicatesStrategy = 'exclude'
        with jar
    }
} else {
    task fatJar(type: Jar) {
        archiveBaseName = project.name + '-all'
        // include all 'api' dependencies
        from {
            configurations.apiResolvable.collect { it.isDirectory() ? it : zipTree(it).matching {
                exclude { it.path.contains('META-INF') }
            }}
        }
        // include compiled source from this project
        from sourceSets.main.allJava
    
        duplicatesStrategy = 'exclude'
        with jar
    }
}

void createAppTask(String taskName, String prefix, String mainClass) {
    // old default prefix was: prefix+'-'+getDate()+'-'+getGitHash()
    if (project.parentProject != null) {
        task (taskName, type: Jar, dependsOn: ':'+project.parentProject+':fatJar') {
            archiveBaseName = prefix
            from { configurations.apiResolvable.collect {
                it.isDirectory() ? it : zipTree(it).matching {
                    exclude { it.path.contains('META-INF') }
                }
            }}
            // include compiled source from this project
            from sourceSets.main.allJava
            // include upstream project fat jar
            from zipTree(file('../'+project.parentProject+'/build/libs/'+project.parentProject+'-all.jar'))
            from(project(':opensha').projectDir) {
                include 'build.version'
            }
            manifest {
                attributes(
                    'Class-Path': configurations.apiResolvable.collect { it.getName() }.join(' '),
                    'Main-Class': mainClass
                )
            }
            with jar
        }
    } else {
        task (taskName, type: Jar) {
            archiveBaseName = prefix
            from { configurations.apiResolvable.collect {
                it.isDirectory() ? it : zipTree(it).matching {
                    exclude { it.path.contains('META-INF') }
                }
            }}
            // include compiled source from this project
            from sourceSets.main.allJava
            from(project.projectDir) {
                include 'build.version'
            }
            manifest {
                attributes(
                    'Class-Path': configurations.apiResolvable.collect { it.getName() }.join(' '),
                    'Main-Class': mainClass
                )
            }
            with jar
        }
    }
}

// make that method visible
ext {
    createAppTask = this.&createAppTask
}
